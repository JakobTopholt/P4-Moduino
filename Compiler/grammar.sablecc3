Package Moduino;

Helpers

    all = [0 .. 0xFFFF];
    lowercase = ['a' .. 'z'];
    uppercase = ['A' .. 'Z'];
    digit = ['0' .. '9'];
    hex_digit = [digit + [['a' .. 'f'] + ['A' .. 'F']]];

    tab = 9;
    cr = 13;
    lf = 10;
    eol = cr lf | cr | lf; // This takes care of different platforms

    not_cr_lf = [all - [cr + lf]];
    not_star = [all - '*'];
    not_star_slash = [not_star - '/'];
    
    escaped_chars = '\n'; //TODO: more escaped chars
    valid_char = [not_cr_lf - [''' + '"']];
    string_char = [valid_char + '''] | escaped_chars | '\"';
    char_char = [valid_char + '"'] | escaped_chars | '\' + ''';

    blank = (' ' | tab | eol)+;

    short_comment = '#' not_cr_lf* eol;
    long_comment = '/*' not_star* '*'+ (not_star_slash not_star* '*'+)* '/';

    letter = lowercase | uppercase | '_' | '$';
    id_part = (uppercase | lowercase)+ (digit)*;

Tokens

    tprog = 'prog';
    tloop = 'loop';
    tfunc = 'func';
    if = 'if';
    else = 'else';
    for = 'for';
    do = 'do';
    while = 'while';
    treturn = 'return';
    tvoid = 'void';
    
    tdelay = 'delay';
    tsetpin = 'setpin';
    low = 'low';
    high = 'high';
    
    tint = 'int';
    tdecimal = 'decimal';
    tbool = 'bool';
    tchar = 'char';
    tstring = 'string';
    tunit = 'unit';
    tvalue = 'value';
    tpin = 'pin';
    
    ttrue = 'true';
    tfalse = 'false';

    l_bkt = '[';
    r_bkt = ']';
    l_par = '(';
    r_par = ')';
    l_brace =  '{';
    r_brace =  '}';
    
    tplusplus = '++';
    tminusminus = '--';
    tplusassign = '+=';
    tminusassign = '-=';
    tslashslash = '//';
    tstarstar = '**';
    tbarbar = '||';
    tand = '&&';
    tequalequal = '==';
    tnotequal = '!=';
    tlessequal = '<=';
    tgreaterequal = '>=';
    tless = '<';
    tgreater = '>';

    plus = '+';
    minus = '-';
    slash = '/';
    star = '*';
    modulo = '%'; 

    equal = '=';
    q_mark = '?';
    bar = '|';
    tamparsand = '&';
    not = '!';
    arrow = '=>';

    comma = ',';
    semicolon = ';';
    colon = ':';
    dot = '.';

    id = id_part ('_' id_part)*;

    decimal = digit+ '.' digit+;
    number = digit+;
    hex = '0' ('x' | 'X') hex_digit+;

    string = '"' string_char+ '"';
    char = ''' char_char+ '''; //TODO: test if string and char works correctly

    blank = blank;
    comment = short_comment | long_comment;


Ignored Tokens
    blank,
    comment;

Productions
    grammar = global_list? {-> New grammar([global_list.global])};
    
    //Any ordered list of functions such as: unit, func, unit, unit, func,
    global_list {-> global*} = global global_list? {-> [global.global, global_list.global]};
    
    global {-> global} = {unitdecl} unitdecl {-> unitdecl.global}
                       | {func} func {-> func.global};
    //// Units
    
    // A unit declaration consist of one unit and it's subunits
    unitdecl {-> global} = tunit id l_brace subunit_list r_brace {-> New global.unitdecl(id, [subunit_list.subunit])};
    
    subunit_list {-> subunit*} = subunit subunit_list? {-> [subunit.subunit, subunit_list.subunit]};
    
    subunit {-> subunit} = id arrow exp semicolon {-> New subunit(id, exp.exp)};
    
    // Distance/Time speed = 50 meters / 10 seconds 
    // Unituse is when the unit is used as the type such as distance/time (speed) or distance/time/time (acceleration), /time*distance and /time/time*distance is also allowed
    
    
    // first unituse is rather special because here the star shouldn't be used
    firstunituse {-> unituse} = {num}   id      {-> New unituse.num(id)}
                              | {usual} tslashslash id {-> New unituse.den(id)};
                              
    unituse_list {-> unituse*} = unituse unituse_list? {-> [unituse.unituse, unituse_list.unituse]};
    
    unituse {-> unituse} = {num}    tstarstar id     {-> New unituse.num(id)}
                         | {den}    tslashslash id    {-> New unituse.den(id)};
    
    // For usage in expressions ex: 50 ms, 5 s, 50 meters
    unitnumber {-> exp} = {unitdecimal} decimal id {-> New exp.unitdecimal(decimal, id)}
                        | {unitnumber} number id {-> New exp.unitnumber(number, id)};
    // Type
    functype {-> type} = {void} tvoid {-> New type.void()}
                       | {unittype} unittype {-> unittype.type};
                       
    unittype {-> type} = {unituse} firstunituse unituse_list?   {-> New type.unit([firstunituse.unituse, unituse_list.unituse])}
                       | {type} type                            {-> type.type};
                           
    type {-> type} = {int}      tint        {-> New type.int()}
                   | {decimal}  tdecimal    {-> New type.decimal()}
                   | {bool}     tbool       {-> New type.bool()}
                   | {char}     tchar       {-> New type.char()}
                   | {string}   tstring     {-> New type.string()}
                   | {pin}      tpin        {-> New type.pin()};

                               
    //// Functions and statements
    // prog {}, loop {}, func a {} or int a {}
    func {-> global} = {prog}     tprog l_brace stmt_list? r_brace         {-> New global.prog([stmt_list.stmt])}
                     | {loop}     tloop l_brace stmt_list? r_brace         {-> New global.loop([stmt_list.stmt])}
                     | {untyped}  tfunc id l_par args? r_par l_brace stmt_list? return? r_brace      {-> New global.untyped(id, [args.arg], [stmt_list.stmt, return.stmt])}
                     | {typed}    functype id l_par args? r_par l_brace stmt_list? return? r_brace   {-> New global.typed(functype.type, id, [args.arg], [stmt_list.stmt, return.stmt])};
            
    //arguments used to declare a function and params when using a function
    params {-> exp*} = {list}   exp comma params   {-> [exp.exp, params.exp]}
                     | {single} exp                 {-> [exp.exp]};
                     
    args {-> arg*} = {list} arg comma args {-> [arg.arg, args.arg]}
                   | {single} arg {-> [arg.arg]};
                   
    arg {-> arg} = unittype id {-> New arg(unittype.type, id)};
    
    // Statements
    stmt_list {-> stmt*} = stmt stmt_list?                      {-> [stmt.stmt, stmt_list.stmt]};
    
    stmt {-> stmt} = {assign}   assign semicolon                        {-> assign.stmt}
                   | {decl}     unittype id semicolon                   {-> New stmt.decl(unittype.type, id)}
                   | {declass}  unittype id equal exp semicolon         {-> New stmt.declass(unittype.type, id, exp.exp)}
                   | {funccall} id l_par params? r_par semicolon        {-> New stmt.funccall(id, [params.exp])}
                   | {delay} tdelay l_par exp r_par semicolon           {-> New stmt.delay(exp.exp)}
                   | {setpin} tsetpin l_par exp comma pintoggle r_par   {-> New stmt.setpin(exp.exp, pintoggle.pintoggle)}
                   | {scoped}   scoped                                  {-> scoped.stmt};
                   
    pintoggle {-> pintoggle} = {high} high  {-> New pintoggle.high()}
                             | {low} low    {-> New pintoggle.low()};
                   
    return {-> stmt} = treturn exp? semicolon               {-> New stmt.return(exp.exp)};
    
    // Needed for for loop for (int i; i < 5;i += 1)
    assign {-> stmt} = {normal}         id equal exp        {-> New stmt.assign(id, exp.exp)}
                     | {plusassign}     id tplusassign exp  {-> New stmt.plusassign(id, exp.exp)}
                     | {minusassign}    id tminusassign exp {-> New stmt.minusassign(id, exp.exp)}
                     | {prefixplus}     tplusplus id        {-> New stmt.prefixplus(id)}
                     | {prefixminus}    tminusminus id      {-> New stmt.prefixminus(id)}
                     | {suffixplus}     id tplusplus        {-> New stmt.suffixplus(id)}
                     | {suffixminus}    id tminusminus      {-> New stmt.suffixminus(id)};
    // scoped/block statements
    scoped {-> stmt} = {if}       if l_par exp r_par l_brace stmt_list? return? r_brace                                              {-> New stmt.if(exp.exp, [stmt_list.stmt, return.stmt])}
                     | {else}     else l_brace stmt_list? return? r_brace                                                            {-> New stmt.else([stmt_list.stmt, return.stmt])}
                     | {elseif}   else if l_par exp r_par l_brace stmt_list? return? r_brace                                         {-> New stmt.elseif(exp.exp, [stmt_list.stmt, return.stmt])}
                     | {for}      for l_par [init]:stmt [cond]:exp semicolon [incre]:assign r_par l_brace stmt_list? return? r_brace {-> New stmt.for(init.stmt, cond.exp, incre.stmt, [stmt_list.stmt, return.stmt])}
                     | {while}    while l_par exp r_par l_brace stmt_list? return? r_brace                                           {-> New stmt.while(exp.exp, [stmt_list.stmt, return.stmt])}
                     | {dowhile}  do l_brace stmt_list? return? r_brace while l_par exp r_par                                        {-> New stmt.dowhile([stmt_list.stmt, return.stmt], exp.exp)};
                           
    //// Expressions - check precedence table from report
    exp {-> exp}= {ternary}         p8 q_mark [true]:exp colon [false]:exp  {-> New exp.ternary(p8.exp, true.exp, false.exp)}
                | p8 {-> p8.exp}; //TODO: right to left override case
    p8 {-> exp} = {or}              p8 tbarbar p7                           {-> New exp.or(p8.exp, p7.exp)}
                | p7 {-> p7.exp};
    p7 {-> exp} = {and}             p7 tand p6                              {-> New exp.and(p7.exp, p6.exp)}
                | p6 {-> p6.exp};
    p6 {-> exp} = {equal}           p6 tequalequal p5                       {-> New exp.equal(p6.exp, p5.exp)}
                | {notequal}        p6 tnotequal p5                         {-> New exp.notequal(p6.exp, p5.exp)}
                | p5 {-> p5.exp};
    p5 {-> exp} = {greater}         p5 tgreater p4                          {-> New exp.greater(p5.exp, p4.exp)}
                | {greaterequal}    p5 tgreaterequal p4                     {-> New exp.greaterequal(p5.exp, p4.exp)}
                | {less}            p5 tless p4                             {-> New exp.less(p5.exp, p4.exp)}
                | {lessequal}       p5 tlessequal p4                        {-> New exp.lessequal(p5.exp, p4.exp)}
                | p4 {-> p4.exp};
    p4 {-> exp} = {plus}            p4 plus p3                              {-> New exp.plus(p4.exp, p3.exp)}
                | {minus}           p4 minus p3                             {-> New exp.minus(p4.exp, p3.exp)}
                | p3 {-> p3.exp};
    p3 {-> exp} = {divide}          p3 slash p2                             {-> New exp.divide(p3.exp, p2.exp)}
                | {multiply}        p3 star p2                              {-> New exp.multiply(p3.exp, p2.exp)}
                | {remainder}       p3 modulo p2                            {-> New exp.remainder(p3.exp, p2.exp)}
                | p2 {-> p2.exp};
    p2 {-> exp} = {suffixplusplus}  p1 tplusplus                            {-> New exp.suffixplusplus(p1.exp)}
                | {suffixminusminus}p1 tminusminus                          {-> New exp.suffixminusminus(p1.exp)}
                | {unaryminus}      minus p1                                {-> New exp.unaryminus(p1.exp)}
                | {logicalnot}      not p1                                  {-> New exp.logicalnot(p1.exp)}
                | {cast}            l_par type r_par p1                     {-> New exp.cast(type.type, p1.exp)}
                | p1 {-> p1.exp}; //TODO: right to left override case
    p1 {-> exp} = {prefixplusplus}  tplusplus p0                            {-> New exp.prefixplusplus(p0.exp)}
                | {prefixminusminus}tminusminus p0                          {-> New exp.prefixminusminus(p0.exp)}
                | {funccall}        id l_par params? r_par                  {-> New exp.funccall(id, [params.exp])} // <- array and structaccess goes here
                | p0 {-> p0.exp};
    p0 {-> exp} = {id}              id {-> New exp.id(id)}
                | {decimal}         decimal                                 {-> New exp.decimal(decimal)}
                | {unit}            unitnumber                              {-> unitnumber.exp}
                | {value}           tvalue                                  {-> New exp.value()}
                | {number}          number                                  {-> New exp.number(number)}
                | {boolean}         boolean                                 {-> New exp.boolean(boolean.boolean)}
                | {string}          string                                  {-> New exp.string(string)}
                | {char}            char                                    {-> New exp.char(char)}
                | {exp}             l_par exp r_par                         {-> exp.exp};
                
    //// Miscellaneous    
    boolean {-> boolean} = {true} ttrue {-> New boolean.true()}
                         | {false} tfalse {-> New boolean.false()};

Abstract Syntax Tree
    grammar = global+;
    
    global = {unitdecl} id subunit+
           | {prog} stmt*
           | {loop} stmt*
           | {untyped} id arg* stmt*
           | {typed} type id arg* stmt*;
    
    subunit = id exp;
    arg = type id;
    
    //stmt in CodeGen
    stmt = {assign}      id exp       
         | {plusassign}  id exp   
         | {minusassign} id exp  
         | {prefixplus}  id             
         | {prefixminus} id            
         | {suffixplus}  id             
         | {suffixminus} id                          
         | {decl} type id
         | {declass} type id exp
         | {funccall} id [params]:exp*
         | {return} exp?
         | {if} exp stmt+
         | {elseif} exp stmt+
         | {else} stmt+
         | {for} [init]:stmt [cond]:exp [incre]:stmt stmt+ // incre is always an assignstmt
         | {while} exp stmt+
         | {dowhile} stmt+ exp
         | {delay} exp
         | {setpin} exp pintoggle;
         
    pintoggle = {high} | {low};
              
    type = {int} 
         | {decimal}
         | {bool}
         | {char}
         | {string}
         | {unit} unituse+
         | {void}
         | {pin};
         
    unituse = {num} id // numerator
            | {den} id; // denumerator
    
    //Expressions
    exp = {ternary}         [cond]:exp [true]:exp [false]:exp
        | {or}              [l]:exp [r]:exp
        | {and}             [l]:exp [r]:exp
        | {equal}           [l]:exp [r]:exp
        | {notequal}        [l]:exp [r]:exp
        | {greater}         [l]:exp [r]:exp
        | {greaterequal}    [l]:exp [r]:exp
        | {less}            [l]:exp [r]:exp
        | {lessequal}       [l]:exp [r]:exp
        | {plus}            [l]:exp [r]:exp
        | {minus}           [l]:exp [r]:exp
        | {divide}          [l]:exp [r]:exp
        | {multiply}        [l]:exp [r]:exp
        | {remainder}       [l]:exp [r]:exp
        | {suffixplusplus}  exp
        | {suffixminusminus}exp
        | {unaryminus}      exp
        | {logicalnot}      exp
        | {cast}            type exp
        | {prefixplusplus}  exp
        | {prefixminusminus}exp
        | {funccall}        id exp*
        | {id}              id
        | {unitnumber}      number id
        | {unitdecimal}     decimal id
        | {decimal}         decimal
        | {value}           
        | {number}          number
        | {boolean}         boolean
        | {string}          string
        | {char}            char
        | {exp}             exp;  
        
    boolean = {true} | {false};
    
// TODO: Arrays
// TODO: Switch
