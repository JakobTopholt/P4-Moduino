Package Moduino;

Helpers

    all = [0 .. 0xFFFF];
    lowercase = ['a' .. 'z'];
    uppercase = ['A' .. 'Z'];
    digit = ['0' .. '9'];
    hex_digit = [digit + [['a' .. 'f'] + ['A' .. 'F']]];

    tab = 9;
    cr = 13;
    lf = 10;
    eol = cr lf | cr | lf; // This takes care of different platforms

    not_cr_lf = [all - [cr + lf]];
    not_star = [all - '*'];
    not_star_slash = [not_star - '/'];

    blank = (' ' | tab | eol)+;

    short_comment = '//' not_cr_lf* eol;
    long_comment =
        '/*' not_star* '*'+ (not_star_slash not_star* '*'+)* '/';
    comment = short_comment | long_comment;

    letter = lowercase | uppercase | '_' | '$';
    id_part = lowercase (lowercase | digit)*;

Tokens

    prog = 'Prog';
    tfunc = 'func';
    if = 'if';
    else = 'else';
    for = 'for';
    do = 'do';
    while = 'while';
    return = 'return';

    tint = 'int';
    tdecimal = 'decimal';
    tbool = 'bool';
    tchar = 'char';
    tstring = 'string';
    tunit = 'unit';

    l_bkt = '[';
    r_bkt = ']';
    l_par = '(';
    r_par = ')';
    l_brace =  '{';
    r_brace =  '}';

    plus = '+';
    minus = '-';
    slash = '/';
    star = '*';

    equal = '=';
    q_mark = '?';
    bar = '|';
    arrow = '=>';

    comma = ',';
    semicolon = ';';
    colon = ':';

    tid = id_part ('_' id_part)*;

    char = ''' not_cr_lf ''';
    number = digit+;
    hex = '0' ('x' | 'X') hex_digit+;

    string = ''' [not_cr_lf - ''']+ ''';

    blank = blank;
    comment = comment;


Ignored Tokens
    blank,
    comment;

Productions
    grammar = unit_list? func_list {-> New grammar([unit_list.unit], [func_list.func])};
    func_list {-> func*} = {list} func func_list {-> [func.func, func_list.func]} 
                         | {single} func {-> [func.func]};
    stmt_list {-> stmt*} = {list} stmt stmt_list {-> [stmt.stmt, stmt_list.stmt]} 
                       | {single} stmt {-> [stmt.stmt]};
    unit_list {-> unit*} = {list} unit unit_list {-> [unit.unit, unit_list.unit]}
                             | {single} unit {-> [unit.unit]};
    subunit_list {-> subunit*} = {list} subunit subunit_list {-> [subunit.subunit, subunit_list.subunit]}
                                   | {single} subunit {-> [subunit.subunit]};
                             
    func {-> func} = {prog} prog l_brace stmt_list r_brace {-> New func.prog([stmt_list.stmt])}
                   | {new} tfunc P.id l_brace stmt_list r_brace {-> New func.new(id.id, [stmt_list.stmt])};
    stmt {-> stmt} = {exp} exp semicolon {-> New stmt.exp(exp.exp)}
                   | {assign} assign semicolon {-> assign.stmt}
                   | {decl} decl semicolon {-> New stmt.decl(decl.decl)}
                   | {funccall} P.id l_par r_par semicolon {-> New stmt.funccall(id.id)}
                   | {return} return exp semicolon {-> New stmt.return(exp.exp)}
                   | {scoped} scoped {-> New stmt.scoped(scoped.scoped)};

    scoped {-> scoped} = {if} if l_par exp r_par l_brace stmt_list r_brace {-> New scoped.if(exp.exp, [stmt_list.stmt])}
                       | {else} else l_brace stmt_list r_brace {-> New scoped.else([stmt_list.stmt])}
                       | {elseif} else if l_par exp r_par l_brace stmt_list r_brace {-> New scoped.elseif(exp.exp, [stmt_list.stmt])}
                       | {for} for l_par [init]:decl [fst]:semicolon [cond]:exp [snd]:semicolon [incre]:assign r_par l_brace stmt_list r_brace {-> New scoped.for(init.decl, cond.exp, incre.stmt, [stmt_list.stmt])}
                       | {while} while l_par exp r_par l_brace stmt_list r_brace {-> New scoped.while(exp.exp, [stmt_list.stmt])}
                       | {dowhile} do l_brace stmt_list r_brace while l_par exp r_par {-> New scoped.dowhile([stmt_list.stmt], exp.exp)};
     
    unit {-> unit} = tunit id colon tint l_brace subunit_list r_brace {-> New unit(id.id, tint, [subunit_list.subunit])};
    subunit {-> subunit} = id arrow stmt {-> New subunit(id.id, stmt.stmt)}; //TODO allow value token and stmt block

    exp {-> exp} = {plus} exp plus factor {-> New exp.plus (exp.exp, factor.exp) }
                 | {minus} exp minus factor {-> New exp.minus (exp.exp, factor.exp) }
                 | {factor} factor {-> factor.exp };
    decl {-> decl} = {int} tint id {-> New decl.int (id.id)}
                   | {decimal} tdecimal id {-> New decl.decimal (id.id)}
                   | {bool} tbool id {-> New decl.bool (id.id)}
                   | {char} tchar id {-> New decl.char (id.id)}
                   | {string} tstring id {-> New decl.string (id.id)};
    assign {-> stmt} = P.id equal exp {-> New stmt.assign(id.id, exp.exp)};

    factor {-> exp} = {mult} factor star term {-> New exp.mult (factor.exp, term.exp) } //TODO: Modulo %
                    | {div} factor slash term {-> New exp.div (factor.exp, term.exp) }
                    | {term} term {-> term.exp };

    term {-> exp} = {exp} l_par exp r_par {-> exp.exp }
                  | {number} number {-> New exp.number(number) };
    id {-> P.id} = tid {-> New id(tid)};


Abstract Syntax Tree
    grammar = unit* func*;
    unit = id tint subunit*;
    subunit = id stmt; //TODO: fix to stmt
    func = {prog} stmt+
         | {new} id stmt+;
    stmt = {exp} exp
         | {assign} id exp
         | {decl} decl
         | {funccall} id
         | {return} exp
         | {scoped} scoped;
    scoped = {if} exp stmt+
           | {else} stmt+
           | {elseif} exp stmt+
           | {for} [init]:decl [cond]:exp [incre]:stmt stmt+
           | {while} exp stmt+
           | {dowhile} stmt+ exp; //TODO: Switch
    exp = {plus}    [l]:exp  [r]:exp 
        | {minus}   [l]:exp  [r]:exp 
        | {div}     [l]:exp  [r]:exp 
        | {mult}    [l]:exp  [r]:exp 
        | {number}  number; // TODO: function with return value
    decl = {int} id
         | {decimal} id
         | {bool} id
         | {char} id
         | {string} id; // TODO: Arrays
    id = tid;
